buildscript {
    ext {
        stremio_core_android_profile = "debug"
        kotlinVersion = "1.6.10"
        pbandkVersion = "0.14.1"
        protobufVersion = "3.21.0"
    }

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.0.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.mozilla.rust-android-gradle:plugin:0.9.0"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.18"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "org.mozilla.rust-android-gradle.rust-android"
apply plugin: "com.google.protobuf"
apply plugin: "maven-publish"

android {
    compileSdkVersion 31
    ndkVersion "21.0.6113669"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 6
        versionName "1.0.5"
    }

    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

cargo {
    module = "./"
    libname = "stremio_core_android"
    targets = ["arm", "arm64", "x86", "x86_64"]
    verbose = true
    profile = rootProject.ext.has("stremio_core_android_profile") ? rootProject.ext.get("stremio_core_android_profile") : "debug"
}

dependencies {
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "pro.streem.pbandk:pbandk-runtime:$pbandkVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        pbandk {
            artifact = "pro.streem.pbandk:protoc-gen-pbandk-jvm:$pbandkVersion:jvm8@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove "java"
            }
            task.plugins {
                pbandk {}
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.github.Stremio'
                artifactId 'stremio-core-kotlin'
                version = android.defaultConfig.versionName
            }
        }

        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == "javaPreCompileDebug" || task.name == "javaPreCompileRelease") {
        task.dependsOn "cargoBuild"
    }
}
